buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
    }
}

apply plugin: 'shadow'
apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.5

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.3.1'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.9'
    compile group: 'com.googlecode.plist', name: 'dd-plist', version: '1.8'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.4'
}


jar.ext {
    javaVersion = System.properties["java.version"]
    javaVendor  = System.properties["java.vendor"]
}

jar {
    manifest {
        attributes  'Application-Name': 'Scalr SSH Launcher',
                    'Implementation-Title': 'SSH Launcher',
                    'Implementation-Version': version,
                    'Main-Class': 'com.scalr.ssh.launcher.SSHLauncherCLI',
                    'Permissions': 'all-permissions',
                    'Codebase': codebase,
                    'Application-Library-Allowable-Codebase': allowableCodebase,
                    'Created-By': "$javaVersion ($javaVendor)",
                    'Manifest-Version': '1.0'//,
                    //'Trusted-Only': 'true'
                    //'Trusted-Library': 'true',
    }
}

shadow {
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

task signedJar (type: Task, dependsOn: shadowJar) << {
    ant.signjar(
        jar: shadow.getShadowJar().getCanonicalPath(),
        keystore: signingKeyStore,
        storepass: signingKeyPass,
        alias: signingKeyAlias,
        strict: true,
        verbose: true
    )
}


task createTestFile(type: Copy) {
    from 'src/templates'
    into "."
    destinationDir projectDir
    include "applet.html.template"
    rename { file -> "applet.html" }
    expand(jarPath: projectDir.toPath().relativize(shadow.getShadowJar().toPath()), version: version)
}

task fullBuild(type: Task, dependsOn: [signedJar, createTestFile])


// Helpers
task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}